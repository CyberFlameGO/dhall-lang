{- Create an XML element value.

```
let XML = ./package.dhall

in  XML.render
    ( XML.element
      { tag = "foo"
      , attributes = XML.emptyAttributes
      , content =
          [ XML.leaf { tag = "bar", attributes = [ XML.attribute "n" "1" ] }
          , XML.leaf { tag = "baz", attributes = [ XML.attribute "n" "2" ] }
          ]
      }
    )

= "<foo><bar n=\"1\"/><baz n=\"2\"/></foo>"
```
-}


let XML =
        ./Type sha256:7391450c05bd375f6190dd0a356f67524eb73cf0956b6401346ed6f287c41f3a
      ? ./Type

let List/map =
        ../List/map sha256:dd845ffb4568d40327f2a817eb42d1c6138b929ca758d50bc33112ef3c885680
      ? ../List/map

let Args =
        { attributes :
            List { mapKey : Text, mapValue : Text }
        , content :
            List XML
        , tag :
            Text
        }
      : Type

let element
    : Args → XML
    =   λ(elem : Args)
      → λ(XML : Type)
      → λ ( xml
          : { text :
                Text → XML
            , element :
                  { attributes :
                      List { mapKey : Text, mapValue : Text }
                  , content :
                      List XML
                  , tag :
                      Text
                  }
                → XML
            }
          )
      → xml.element
        { attributes =
            elem.attributes
        , tag =
            elem.tag
        , content =
            List/map XML@1 XML (λ(x : XML@1) → x XML xml) elem.content
        }

in  element
