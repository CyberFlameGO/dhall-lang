{- Create a Text value to be inserted into an XML element as content.

```
let XML = ./package.dhall

in  XML.render
    ( XML.node
      { tag = "location"
      , attributes = XML.emptyAttributes
      , content = [ XML.text "/foo/bar" ]
      }
    )
= "<location>/foo/bar</location>"
```
-}

let XML =
        ./Type sha256:7391450c05bd375f6190dd0a356f67524eb73cf0956b6401346ed6f287c41f3a
      ? ./Type

let text
    : Text → XML
    =   λ(d : Text)
      → λ(XML : Type)
      → λ ( xml
          : { text :
                Text → XML
            , element :
                  { attributes :
                      List { mapKey : Text, mapValue : Text }
                  , content :
                      List XML
                  , tag :
                      Text
                  }
                → XML
            }
          )
      → xml.text d

in  text
